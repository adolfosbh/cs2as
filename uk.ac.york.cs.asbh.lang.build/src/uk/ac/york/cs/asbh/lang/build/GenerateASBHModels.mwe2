/*******************************************************************************
 * Copyright (c) 2015 Willink Transformations Ltd., University of York and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     Adolfo Sanchez-Barbudo Herrera (University of York) - initial API and implementation
 * 
 *******************************************************************************/
module org.eclipse.qvtd.cs2as.compiler.tests.GenerateASBHModels

import org.eclipse.ocl.examples.build.utilities.*
import org.eclipse.ocl.examples.build.xtend.*
import org.eclipse.emf.mwe.utils.*
import org.eclipse.emf.ecore.resource.impl.*

var projectName = "uk.ac.york.cs.asbh.lang.cs2as"
var modelsFolder = "model"
var asPackageName = "uk.ac.york.cs.asbh.lang.cs2as.target"
var asVisitorsPackageName = "${asPackageName}.util"
var asVisitorName = "Visitor"
var asVisitableName = "Visitable"

/**
 *	Generate the Pivot Model, by performing the merge to create the flattened package
 *  with additional MDT/OCL content, alphabeticsing the classes, reloading the genmodel
 *  and finally generating the model code.
 */
Workflow {
	bean = ResourceSetImpl : resourceSet {}
    bean = ProjectMapSetup{ resourceSet = resourceSet}
    bean = EcoreGenModelSetup { resourceSet = resourceSet }
    
	component = GenerateModel { resourceSet = resourceSet skipOnErrors = true
    	genModel = "/${projectName}/${modelsFolder}/SourceMM1.genmodel"
    }
    
    component = GenerateModel { resourceSet = resourceSet skipOnErrors = true
    	genModel = "/${projectName}/${modelsFolder}/TargetMM1.genmodel"
    }
    
    component = GenerateModel { resourceSet = resourceSet skipOnErrors = true
    	genModel = "/${projectName}/${modelsFolder}/EnvExample1.genmodel"
    }

    component = GenerateASVisitors { resourceSet = resourceSet
    	projectName = projectName
    	projectPrefix = "Target"
    	superProjectName = ""
    	genModelFile = "${modelsFolder}/TargetMM1.genmodel"
    	modelPackageName = asPackageName
    	visitorPackageName = asVisitorsPackageName
    	visitorClassName = asVisitorName
    	visitableClassName = asVisitableName
    }

    component = @SubGenerateAutoASModel { resourceSet = resourceSet
    	projectName = projectName
    	modelName = "target"
    	projectPrefix = "Target"
    	superProjectName = ""
    	superModelName = ""
    	genModelFile = "${modelsFolder}/TargetMM1.genmodel"
    	lookupFilePath = "${modelsFolder}/TargetLookup.ocl"
		visitorPackageName = asVisitorsPackageName
		visitorClassName = asVisitorName
    	visitableClassName = asVisitableName
    }
}


