grammar uk.ac.york.cs.cs2as.CS2ASDSL with org.eclipse.ocl.xtext.essentialocl.EssentialOCL

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "platform:/resource/org.eclipse.ocl.pivot/model/Pivot.ecore" as pivot
import "platform:/resource/org.eclipse.ocl.xtext.base/model/BaseCS.ecore" as base
import "platform:/resource/org.eclipse.ocl.xtext.essentialocl/model/EssentialOCLCS.ecore" as essentialocl

generate cs2as_dsl "http://www.ac.uk/york/cs/cs2as/CS2ASDSL"

Model:
	csDecl=CSDecl
	asDecl=ASDecl
	
	(mappingSect=MappingSect & disambiguationSect=DisambiguationSect & nameresoSect=NameResolutionSect )
	
;

CSDecl:
	'source' metamodels+=ImportCS (',' metamodels+=ImportCS)*
;

ASDecl:
	'target' metamodels+=ImportCS (',' metamodels+=ImportCS)*
;

ImportCS returns base::ImportCS:
	(name=Identifier ':')? ownedPathName=URIPathNameCS (isAll?='::*')?
;

MappingSect:
	{MappingSect}
	'mappings' '{'
		mappings+=ClassMap*
	'}'	
;
ClassMap:
	'map' to=SIMPLE_ID 'from'  from=SIMPLE_ID '{'
		('when' rule=ExpCS ';')?
		statements += ClassMapStmnt*
	'}'
;

ClassMapStmnt:
	PropertyMap
	';'
;

PropertyMap:
	(redefine?='redefine')? propName=SIMPLE_ID ':=' propInit=ExpCS
;

DisambiguationSect:
	{DisambiguationSect}
	'disambiguation' '{'
		disambiguations+=ClassDisambiguation*
	'}'
;

ClassDisambiguation:
	class=SIMPLE_ID '{'
		statements+=ClassDisambiguationStmnt*
		
	'}'
;

ClassDisambiguationStmnt:
	DisambiguationDef
	';'
;

DisambiguationDef:
	name=SIMPLE_ID ':=' exp=ExpCS
;

NameResolutionSect:
	{NameResolutionSect}
	'nameresolution' '{'
		('named-element' nameElement=SIMPLE_ID 'name-property' nameProperty=NameExpCS ';')?
		('name-qualifier' nameQualifier=SIMPLE_ID ';')?
		nameResolutions+=ClassNameResolution*
	'}'
;

ClassNameResolution:
	class=SIMPLE_ID '{'
		statements+=ClassNameResolutionStmnt*
	'}'
;

ClassNameResolutionStmnt:
	(NamedElementDef | ScopeDef)
	';'
;

NamedElementDef:
	{NamedElementDef}
	'named-element' ('name-property' namePoperty=NameExpCS)? ('qualified-by' qualifier=SIMPLE_ID)?
;

ScopeDef:
	('nested' | sameScope?= 'same')? 
	'scopes' contributingProp=ExpCS ('['typeFilter=TypeExpCS']')? 
	('to' ('all' | 'nexts' | (propagatingProp+=ExpCS (',' propagatingProp+=ExpCS)* ) ))?
;


/* A primary expression identifies the basic expressions from which more complex expressions may be constructed. */
PrimaryExpCS returns essentialocl::ExpCS:
	NestedExpCS
|	IfExpCS
| 	SelfExpCS
| 	PrimitiveLiteralExpCS
| 	TupleLiteralExpCS
| 	MapLiteralExpCS
| 	CollectionLiteralExpCS
| 	LambdaLiteralExpCS
| 	TypeLiteralExpCS
| 	NameExpCS
|	ResolveExpCS	// DSL addition
|	LookupExpCS;	// DSL addition

ResolveExpCS:
	{ResolveExpCS}
	'resolve'
;


LookupExpCS:
	'lookup' '(' args+=NavigatingArgExpCS (',' args+=NavigatingArgExpCS)* ')'
;